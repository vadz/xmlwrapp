@PACKAGE_INIT@

# - Config file for Xmlwrapp package
# It defines the following variables
#
# XMLWRAPP_FOUND            - True if found otherwise all other vars are undefined
# XMLWRAPP_XSLT_FOUND       - True if xsltwrapp library component was found
# XMLWRAPP_VERSION          - Xmlwrapp release version
# XMLWRAPP_MAJOR            - Xmlwrapp major version number (X in X.y.z)
# XMLWRAPP_MINOR            - Xmlwrapp minor version number (Y in x.Y.z)
# XMLWRAPP_PATCH            - Xmlwrapp patch version number (Z in x.y.Z)
#
# XMLWRAPP_INCLUDE_DIR      - Xmlwrapp include directory
# XMLWRAPP_LIBRARY_DIR      - Link directories for xmlwrapp
# XMLWRAPP_LIBRARIES        - Component libraries xmlwrapp and xsltwrapp (if built)
# XMLWRAPP_XSLT_LIBRARIES   - Component libraries for xsltwrapp only
# XMLWRAPP_XML_LIBRARIES    - Component libraries for xmlwrapp only
#
# Compute paths
set_and_check(XMLWRAPP_INCLUDE_DIR "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@")
set_and_check(XMLWRAPP_LIBRARY_DIR "@PACKAGE_CMAKE_INSTALL_LIBDIR@")
set(XMLWRAPP_XSLT_FOUND "@XMLWRAPP_WITH_LIBXSLT@")

find_package(PkgConfig)
pkg_search_module(LIBXML2 REQUIRED libxml-2.0 >= 2.4.28)
if(XMLWRAPP_XSLT_FOUND)
  pkg_search_module(LIBXSLT REQUIRED libxslt >= 1.1.6)
  pkg_search_module(LIBEXSLT REQUIRED libexslt)
endif(XMLWRAPP_XSLT_FOUND)

# Our library dependencies (contains definitions form IMPORTED targets)
include("${CMAKE_CURRENT_LIST_DIR}/XmlwrappTargets.cmake")

# These are imported targets created by XmlwrappTargets.cmake
set(XMLWRAPP_XML_LIBRARIES xmlwrapp)
if(XMLWRAPP_XSLT_FOUND)
  set(XMLWRAPP_LIBRARIES xmlwrapp xsltwrapp)
  set(XMLWRAPP_XSLT_LIBRARIES xsltwrapp)
else()
  set(XMLWRAPP_LIBRARIES xmlwrapp)
endif(XMLWRAPP_XSLT_FOUND)
